/*
 * Copyright 2023 wcaokaze
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

pluginManagement {
   repositories {
      mavenCentral()
      google()
   }
}

dependencyResolutionManagement {
   repositoriesMode.set(RepositoriesMode.PREFER_PROJECT)
   repositories {
      mavenCentral()
      google()
   }

   versionCatalogs {
      libs {
         version('kotlin', '1.9.21')
         version('android-plugin', '8.2.0')
         version('compose-jb', '1.5.11')
         version('compose-androidx', '1.5.4')
         version('compose-compiler', '1.5.7')

         plugin('kotlin-multiplatform', 'org.jetbrains.kotlin.multiplatform').versionRef('kotlin')
         plugin('kotlinx-serialization', 'org.jetbrains.kotlin.plugin.serialization').versionRef('kotlin')
         plugin('android-library', 'com.android.library').versionRef('android-plugin')
         plugin('compose-jb', 'org.jetbrains.compose').versionRef('compose-jb')

         library('kotlinx-serialization', 'org.jetbrains.kotlinx', 'kotlinx-serialization-json').version('1.6.2')
         library('android-desugaring-jdk', 'com.android.tools', 'desugar_jdk_libs').version('1.2.2')

         library('kotlin-test-junit', 'org.jetbrains.kotlin', 'kotlin-test-junit').versionRef('kotlin')
         library('androidx-test-ext', 'androidx.test.ext', 'junit').version('1.1.5')
         library('compose-ui-test-manifest', 'androidx.compose.ui', 'ui-test-manifest').versionRef('compose-androidx')
      }
   }
}

rootProject.name = "panoptiqon-compose"

include 'panoptiqon'
project(':panoptiqon').projectDir = file('../panoptiqon')
